plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'nu.studer.jooq' version '6.0'
}

group 'de.kittybot'
mainClassName = 'de.kittybot.kittybot.main.Main'

repositories {
    maven { url 'https://m2.dv8tion.net/releases' }
    maven { url 'https://jitpack.io' }
    mavenCentral()
    jcenter() // this is still required because of jda utilities
}

dependencies {
    // discord/jda related
    implementation('net.dv8tion:JDA:4.3.0_295') {
        exclude group: 'club.minnced', module: 'opus-java'
    }
    implementation 'com.jagrosh:jda-utilities:3.0.5'
    implementation 'club.minnced:discord-webhooks:0.5.7'
    implementation 'dev.mlnr:BotListHandler-jda:2.0.0_7'

    // audio
    implementation('com.github.KittyBot-Org:Lavalink-Client:176ca86') {
        exclude group: 'com.sedmelluq', module: 'lavaplayer'
    }
    implementation 'com.sedmelluq:lavaplayer:1.3.78'
    implementation 'se.michaelthelin.spotify:spotify-web-api-java:6.5.4'

    // database
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'org.jooq:jooq:3.15.0'
    implementation 'org.postgresql:postgresql:42.2.23'
    jooqGenerator 'org.postgresql:postgresql:42.2.23'

    // logging
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    implementation 'io.sentry:sentry-logback:5.0.1'

    // eval
    implementation 'org.codehaus.groovy:groovy-jsr223:3.0.8'

    // jjwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.2'

    // Prometheus
    implementation 'io.prometheus:simpleclient:0.11.0'
    implementation 'io.prometheus:simpleclient_hotspot:0.11.0'
    implementation 'io.prometheus:simpleclient_httpserver:0.11.0'

    // other
    implementation 'io.javalin:javalin:3.13.9'
    implementation 'io.github.classgraph:classgraph:4.8.110'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.0.3'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
}

jooq {
    version = '3.14.11'
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = "jdbc:postgresql://${KITTYBOT_DB_HOST}:${KITTYBOT_DB_PORT}/${KITTYBOT_DB_DATABASE}"
                    user = KITTYBOT_DB_USER
                    password = KITTYBOT_DB_PASSWORD
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''
                    }
                    generate {
                        records = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'de.kittybot.kittybot.jooq'
                        directory = "src/main/jooq"
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

ext {
    moduleName = 'KittyBot'
}

sourceCompatibility = 1.11
compileJava.options.encoding = 'UTF-8'
