buildscript {
    repositories {
        maven {
            url "https://dl.bintray.com/kittybot-org/gradle-jooq-plugin"
        }
    }
    dependencies {
        classpath 'de.kittybot.jooq:gradle-jooq-plugin:5.1.7'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply plugin: 'de.kittybot.jooq'

group 'de.kittybot'
mainClassName = 'de.kittybot.kittybot.KittyBot'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // discord/jda related
    implementation 'net.dv8tion:JDA:4.2.0_208'
    implementation 'com.jagrosh:jda-utilities:3.0.4'

    // audio
    implementation("com.github.KittyBot-Org:Lavalink-Client:40ee407472") {
        exclude group: 'com.sedmelluq', module: 'lavaplayer'
    }
    implementation 'com.github.devoxin:lavaplayer:1.3.54.3'
    implementation 'com.sedmelluq:jda-nas:1.1.0'

    // database
    implementation 'com.zaxxer:HikariCP:3.4.5'
    implementation 'org.postgresql:postgresql:42.2.17'
    implementation 'org.jooq:jooq:3.13.5'
    jooqGenerator 'org.postgresql:postgresql:42.2.16'

    // logging
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    implementation 'io.sentry:sentry-logback:3.0.0'

    // eval
    implementation 'org.codehaus.groovy:groovy-jsr223:3.0.6'

    // other
    implementation 'org.discordbots:DBL-Java-Library:2.0.1'
    implementation 'io.javalin:javalin:3.11.0'
    implementation 'org.yaml:snakeyaml:1.26'
    implementation 'io.github.classgraph:classgraph:4.8.78'
}

jooq {
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    url = "jdbc:postgresql://${System.getenv('KITTYBOT_DB_HOST')}:${System.getenv('KITTYBOT_DB_PORT')}/${System.getenv('KITTYBOT_DB_DATABASE')}"
                    user = System.getenv('KITTYBOT_DB_USER')
                    password = System.getenv('KITTYBOT_DB_PASSWORD')
                    driver = 'org.postgresql.Driver'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'de.kittybot.kittybot.database.jooq'
                        directory = "${projectDir}/src/main/java"
                        clean = false
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

ext {
    moduleName = 'KittyBot'
}

compileJava.options.encoding = 'UTF-8'