buildscript {
    repositories {
        maven {
            url "https://dl.bintray.com/kittybot-org/gradle-jooq-plugin"
        }
    }
    dependencies {
        classpath 'de.kittybot.jooq:gradle-jooq-plugin:5.2.7'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply plugin: 'de.kittybot.jooq'

group 'de.kittybot'
mainClassName = 'de.kittybot.kittybot.main.Main'

repositories {
    maven { url 'https://m2.dv8tion.net/releases' }
    mavenCentral() // this is to receive updates from mvnrepository repos that are not synced with jcenter
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // discord/jda related
    implementation('net.dv8tion:JDA:4.2.1_259') {
        exclude group: 'club.minnced', module: 'opus-java'
    }
    implementation 'com.jagrosh:jda-utilities:3.0.5'
    implementation 'club.minnced:discord-webhooks:0.5.6'
    implementation 'com.github.caneleex:BotListHandler:1.0.12'

    // audio
    implementation('com.github.KittyBot-Org:Lavalink-Client:176ca86') {
        exclude group: 'com.sedmelluq', module: 'lavaplayer'
    }
    implementation 'com.sedmelluq:lavaplayer:1.3.76'
    implementation 'se.michaelthelin.spotify:spotify-web-api-java:6.5.4'

    // database
    implementation 'com.zaxxer:HikariCP:4.0.3'
    implementation 'org.jooq:jooq:3.14.8'
    implementation 'org.postgresql:postgresql:42.2.19'
    jooqGenerator 'org.postgresql:postgresql:42.2.18'

    // logging
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    implementation 'io.sentry:sentry-logback:4.3.0'

    // eval
    implementation 'org.codehaus.groovy:groovy-jsr223:3.0.8'

    // jjwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.2'

    // Prometheus
    implementation 'io.prometheus:simpleclient:0.10.0'
    implementation 'io.prometheus:simpleclient_hotspot:0.10.0'
    implementation 'io.prometheus:simpleclient_httpserver:0.10.0'

    // other
    implementation 'io.javalin:javalin:3.13.6'
    implementation 'io.github.classgraph:classgraph:4.8.104'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.8'
    implementation 'com.squareup.okhttp3:okhttp:4.9.1'
}

jooq {
    version = '3.14.4'
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = "jdbc:postgresql://${KITTYBOT_DB_HOST}:${KITTYBOT_DB_PORT}/${KITTYBOT_DB_DATABASE}"
                    user = KITTYBOT_DB_USER
                    password = KITTYBOT_DB_PASSWORD
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''
                    }
                    generate {
                        records = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'de.kittybot.kittybot.jooq'
                        directory = "src/main/jooq"
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

ext {
    moduleName = 'KittyBot'
}

sourceCompatibility = 1.11
compileJava.options.encoding = 'UTF-8'
