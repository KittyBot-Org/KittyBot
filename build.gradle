buildscript {
    repositories {
        maven {
            url "https://dl.bintray.com/kittybot-org/gradle-jooq-plugin"
        }
    }
    dependencies {
        classpath 'de.kittybot.jooq:gradle-jooq-plugin:5.2.9.3'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply plugin: 'de.kittybot.jooq'

group 'de.kittybot'
mainClassName = 'de.kittybot.kittybot.KittyBot'

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // discord/jda related
    implementation 'net.dv8tion:JDA:4.2.0_215'
    implementation 'com.jagrosh:jda-utilities:3.0.5'

    // audio
    implementation("com.github.KittyBot-Org:Lavalink-Client:f7842dfdb4") {
        exclude group: 'com.sedmelluq', module: 'lavaplayer'
    }
    implementation 'com.sedmelluq:lavaplayer:1.3.60'
    implementation 'com.sedmelluq:jda-nas:1.1.0'

    // database
    implementation 'com.zaxxer:HikariCP:3.4.5'
    implementation 'org.jooq:jooq:3.14.3'

    implementation 'org.postgresql:postgresql:42.2.18'
    jooqGenerator 'org.postgresql:postgresql:42.2.18'

    // logging
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    implementation 'io.sentry:sentry-logback:3.1.3'

    // eval
    implementation 'org.codehaus.groovy:groovy-jsr223:3.0.6'

    // jjwt
    implementation 'io.jsonwebtoken:jjwt-api:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-impl:0.11.2'
    implementation 'io.jsonwebtoken:jjwt-jackson:0.11.2'

    // other
    implementation 'io.javalin:javalin:3.11.2'
    implementation 'io.github.classgraph:classgraph:4.8.90'
}

jooq {
    version = '3.14.3'
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    url = "jdbc:postgresql://${KITTYBOT_DB_HOST}:${KITTYBOT_DB_PORT}/${KITTYBOT_DB_DATABASE}"
                    user = KITTYBOT_DB_USER
                    password = KITTYBOT_DB_PASSWORD
                    driver = 'org.postgresql.Driver'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'de.kittybot.kittybot.database.jooq'
                        directory = "${projectDir}/src/main/java"
                        clean = true
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

ext {
    moduleName = 'KittyBot'
}

compileJava.options.encoding = 'UTF-8'
