buildscript {
    repositories {
        maven {
            url "https://dl.bintray.com/kittybot-org/gradle-jooq-plugin"
        }
    }
    dependencies {
        classpath 'de.kittybot.jooq:gradle-jooq-plugin:5.1.7'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'com.gorylenko.gradle-git-properties' version '2.2.3'
    id 'org.ajoberstar.grgit' version '4.1.0'
}

apply plugin: 'de.kittybot.jooq'

group 'de.kittybot'
mainClassName = 'de.kittybot.kittybot.KittyBot'
version = "${versionFromTag()}".toString()

repositories {
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // discord/jda related
    implementation 'net.dv8tion:JDA:4.2.0_206'
    implementation 'com.jagrosh:jda-utilities:3.0.4'

    // audio
    implementation 'com.github.devoxin:lavaplayer:a63c541dc5'
    implementation 'com.sedmelluq:jda-nas:1.1.0'
    implementation("com.github.KittyBot-Org:Lavalink-Client:2f8d745521") {
        exclude group: 'com.sedmelluq', module: 'lavaplayer'
    }

    // database
    implementation 'com.zaxxer:HikariCP:3.4.5'
    implementation 'org.postgresql:postgresql:42.2.16'
    implementation 'org.jooq:jooq:3.13.4'
    jooqGenerator 'org.postgresql:postgresql:42.2.16'

    // logging
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    implementation 'io.sentry:sentry-logback:1.7.30'

    // script engine
    implementation 'org.graalvm.js:js:20.2.0'
    implementation 'org.graalvm.js:js-scriptengine:20.2.0'

    // other
    implementation 'org.discordbots:DBL-Java-Library:2.0.1'
    implementation 'io.javalin:javalin:3.10.1'
    implementation 'org.yaml:snakeyaml:1.26'
    implementation 'io.github.classgraph:classgraph:4.8.78'
}

jooq {
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    url = "jdbc:postgresql://${System.getenv('KITTYBOT_DB_HOST')}:${System.getenv('KITTYBOT_DB_PORT')}/${System.getenv('KITTYBOT_DB_DATABASE')}"
                    user = System.getenv('KITTYBOT_DB_USER')
                    password = System.getenv('KITTYBOT_DB_PASSWORD')
                    driver = 'org.postgresql.Driver'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        inputSchema = 'public'
                        includes = '.*'
                        excludes = ''
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = true
                        fluentSetters = true
                    }
                    target {
                        packageName = 'de.kittybot.kittybot.database.jooq'
                        directory = "${projectDir}/src/main/java"
                        clean = false
                    }
                    strategy.name = "org.jooq.codegen.DefaultGeneratorStrategy"
                }
            }
        }
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

ext {
    moduleName = 'KittyBot'
}

processResources {
    //inject values into app.properties
    filesMatching("**/app.properties") {
        filter ReplaceTokens, tokens: [
                "project.version"   : project.version,
                "project.groupId"   : project.group,
                "project.artifactId": project.ext.moduleName,
                "env.BUILD_NUMBER"  : (System.getenv('CI') ? System.getenv('BUILD_NUMBER') : 'Unofficial'),
                "env.BUILD_TIME"    : System.currentTimeMillis() + ''
        ]
    }
}

build {
    doLast {
        println 'Version: ' + version
    }
}

@SuppressWarnings("GrMethodMayBeStatic")
String versionFromTag() {

    def headTag = grgit.tag.list().find {
        it.commit.getId() == grgit.head().getId()
    }

    // Uncommitted changes? -> should be SNAPSHOT
    // Also watch out for false positives in the CI build
    def clean = grgit.status().clean || System.getenv('CI') != null

    if (!clean) {
        println("Git state is dirty, setting version as snapshot")
    }

    if (headTag && clean) {
        headTag.getName()
    } else {
        "${grgit.head().id}-SNAPSHOT"
    }
}

void versionTxt() {
    new File("$projectDir/VERSION.txt").text = "$project.version\n"
}

versionTxt()

compileJava.options.encoding = 'UTF-8'